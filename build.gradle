plugins {
	id 'java'
	id "org.springframework.boot" version "${springBootVersion}"
	id "io.spring.dependency-management" version "${springDependencyVersion}"
}

bootJar {
	enabled = true
	mainClass = 'com.kakao.Application'
}
jar { enabled = false }


springBoot {
	mainClass = 'com.kakao.Application'
}

group = 'com.kakao'
version = '0.0.1-SNAPSHOT'

compileJava {
	options.encoding = 'UTF-8'
	options.compilerArgs = [ '-parameters' ]
}

configurations.configureEach {
	resolutionStrategy.cacheChangingModulesFor 30, 'seconds'
	resolutionStrategy.cacheDynamicVersionsFor 30, 'seconds'
}

repositories {
	mavenCentral()

	maven {
		url "https://plugins.gradle.org/m2/"
	}
}

dependencies {
	/**  Springframework  **/
	implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation ('org.springframework.boot:spring-boot-starter-web') {
		exclude module: "spring-boot-starter-tomcat"
	}
	implementation ('org.springframework.boot:spring-boot-starter-undertow') {
		exclude group: 'io.undertow' , module:'undertow-websockets-jsr'
	}
	implementation "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'com.oracle.database.spring:oracle-spring-boot-starter-ucp:3.1.0'
	implementation "org.apache.commons:commons-lang3:${apacheCommTool}"

	/** swagger **/
	implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"



	implementation project(':onlinecore')
	implementation project(':api.interface')
	implementation project(':api.common')
	implementation project(':api.deposit')
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	compileTestJava.options.encoding = 'UTF-8'

	compileJava {
		options.encoding = 'UTF-8'
		options.compilerArgs = ['-parameters']
	}

	tasks.withType(Jar).configureEach {
		destinationDirectory.set(file("$project.parent.projectDir/libs"))
	}

	configurations.configureEach {
		resolutionStrategy.cacheChangingModulesFor 30, 'seconds'
		resolutionStrategy.cacheDynamicVersionsFor 30, 'seconds'
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation ('org.springframework.boot:spring-boot-starter-web') {
			exclude module: "spring-boot-starter-tomcat"
		}
		implementation ('org.springframework.boot:spring-boot-starter-undertow') {
			exclude group: 'io.undertow' , module:'undertow-websockets-jsr'
		}
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
		implementation 'com.oracle.database.spring:oracle-spring-boot-starter-ucp:3.1.0'
		implementation "org.apache.commons:commons-lang3:${apacheCommTool}"

		implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"

	}
}
